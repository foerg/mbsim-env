@echo off
rem Windows batch variant of mbsimxml (mbsimxml.bat.in)
rem If editing this file, also edit the file mbsimxml.in!

set bindir=@win_bindir@
set MBSIMSCHEMA=@win_MBSIMSCHEMA@
set MBSIMXMLSCHEMA=@win_MBSIMXMLSCHEMA@
set MBXMLUTILSBINDIR=@win_MBXMLUTILSBINDIR@

rem help
if %1!==! goto help
if "%1"=="-h" goto help
if "%1"=="--help" goto help
goto help_cont
:help
  echo "Usage: mbsimxml [--mbsimparam <mbsimparameterfile>] <mbsimfile>"
  echo "                [--intparam <integratorparameterfile>] <integratorfile>"
  echo ""
  echo "Copyright (C) 2004-2009 MBSim Development Team"
  echo "This is free software; see the source for copying conditions. There is NO"
  echo "warranty; not even for MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE."
  echo ""
  echo "Licensed under the GNU Lesser General Public License (LGPL)"
  goto end
:help_cont

rem parameters
if "%1" == "--mbsimparam" (
  set PARAM=%2
  shift
  shift
) else (
  set PARAM=none
)
set MBSIM=%1
set PPMBSIM=%~n1
set PPMBSIM=.pp.%PPMBSIM%.xml
shift
if "%1" == "--intparam" (
  set PARAMINT=%2
  shift
  shift
) else (
  set PARAMINT=none
)
set MBSIMINT=%1
set PPMBSIMINT=%~n1
set PPMBSIMINT=.pp.%PPMBSIMINT%.xml

del %PPMBSIM% %PPMBSIMINT%

%MBXMLUTILSBINDIR%/mbxmlutilspp %PARAM% %MBSIM% %MBSIMXMLSCHEMA%/mbsimxml.xsd %PARAMINT% %MBSIMINT% %MBSIMSCHEMA%/mbsimintegrator.xsd || goto end

%bindir%/mbsimflatxml %PPMBSIM% %PPMBSIMINT% || goto end

:end
